/*
 */
 
apply plugin: 'java'

repositories {
	jcenter()
	maven { url 'https://dl.bintray.com/content/simonpoole/osm' }
}

ext {
	preset_file = 'master_preset.xml'
	josm_preset_file = 'josm_preset.xml'
	vespucci_preset_file = 'vespucci_preset.xml'
	vespucci_preset_build_file = 'vespucci_preset_build.xml'
    vespucci_preset_version = '1.4'
}

dependencies {
	runtime 'ch.poole:preset-utils:0.8.0'
}

defaultTasks 'generateAllPresetTypes'

task copyCss(type: Copy) {
	from zipTree(project.configurations.runtime.filter{it.name.startsWith('preset-utils')}.singleFile)
    include "preset.css"
    into new File(projectDir.getPath() + '/website')
}

task copyResources(type: Copy) {
	from zipTree(project.configurations.runtime.filter{it.name.startsWith('preset-utils')}.singleFile)
    include "josm-preset-1.0.xlmns"
    include "vespucci-preset-${vespucci_preset_version}.xlmns"
	include "toJOSM.xslt"
    include "href2wiki.xslt"
    include "wiki2href.xslt"
    into new File(buildDir.getPath() + '/xml')
}

task updateWebsite(dependsOn: ["copyCss"], type: JavaExec) {
    main = "ch.poole.osm.presetutils.Preset2Html"
    classpath = configurations.runtime
    args('-input',    preset_file,
    	 '-output',   'index.html',
    	 '-vespucci', 'https://github.com/simonpoole/' + project.name + '/raw/master/gen/vespucci_zip.zip', 
    	 '-josm',     'https://github.com/simonpoole/' + project.name + '/raw/master/gen/josm.zip')
}
updateWebsite.group = 'preset utils'
updateWebsite.description = 'Update the website'

task updatePot(type: JavaExec) {
    main = "ch.poole.osm.presetutils.Preset2Pot"
    classpath = configurations.runtime
    args('-input',  preset_file,
    	 '-output', 'i18n/preset.pot')
}
updatePot.group = 'preset utils'
updatePot.description = 'Update the translation template'

task updateTranslations(type: Exec) {
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    	commandLine 'cmd', '/c', 'tx.exe', 'pull', '-a'
	} else {
    	commandLine 'tx', 'pull', '-a'
	}
}
updateTranslations.group = 'transifex'
updateTranslations.description = 'Update translations by executing the transifex tx utility'

// one time transformation to new format
task transformHrefToWiki(type: Exec) {
    def output = 'build/temp/no_href.xml'
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        // remove extensions
        commandLine 'cmd', '/c', 'xmlstarlet', 'tr', 'build/xml/href2wiki.xslt', 'master_preset.xml'
    } else {
        // remove extensions
        commandLine 'xmlstarlet', 'tr', 'build/xml/href2wiki.xslt', 'master_preset.xml'
    }
    doFirst {
        standardOutput = new FileOutputStream(new File(projectDir, output))
    }
}

// tasks to generate a zipped preset for JOSM
task josmIcons(type: Copy) {
  from projectDir
  include preset_file
  into 'build/temp'
  filteringCharset = 'UTF-8'
  rename preset_file, josm_preset_file
  expand([
    ICONPATH: '',
    ICONTYPE: 'png'
  ])
}

task transformToJosm(dependsOn: ["josmIcons","copyResources"], type: Exec) {
	def output = 'gen/' + josm_preset_file
	mkdir('gen')
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
		// remove extensions
    	commandLine 'cmd', '/c', 'xmlstarlet', 'tr', 'build/xml/toJOSM.xslt', 'build/temp/' + josm_preset_file 
	} else {
		// remove extensions
    	commandLine 'xmlstarlet', 'tr', 'build/xml/toJOSM.xslt', 'build/temp/' + josm_preset_file
	}	
	doFirst {
		standardOutput = new FileOutputStream(new File(projectDir, output))
	}
}

task generateAndValidateJosm(dependsOn: ["transformToJosm"], type: Exec) {
	def input = 'gen/' + josm_preset_file
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    	// validation
    	commandLine 'cmd', '/c', 'xmlstarlet', 'val', '-s', 'build/xml/josm-preset-1.0.xlmns', '-e', input
	} else {
    	// validation
    	commandLine 'xmlstarlet', 'val', '-s', 'build/xml/josm-preset-1.0.xlmns', '-e', input
	}	
}

task josmZip(dependsOn: ["generateAndValidateJosm"], type: Zip) {
    baseName 'josm'
    destinationDir = new File(projectDir, 'gen')
    from ('icons/png')
    from ('gen/' + josm_preset_file)
}
josmZip.group = 'preset'
josmZip.description = 'Generate zipped preset file for JOSM'

// tasks to generate a zipped preset for vespucci
task vespucciIcons(type: Copy) {
  from projectDir
  include preset_file
  into 'gen'
  filteringCharset = 'UTF-8'
  rename (preset_file, vespucci_preset_file)
  expand([
    ICONPATH: '',
    ICONTYPE: 'png'
  ])
}

task generateAndValidateVespucci(dependsOn: ["vespucciIcons","copyResources"], type: Exec) {
	def input = 'gen/' + vespucci_preset_file
	mkdir('gen')
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    	// validation
    	commandLine 'cmd', '/c', 'xmlstarlet', 'val', '-s', "build/xml/vespucci-preset-${vespucci_preset_version}.xlmns", '-e', input
	} else {
    	// validation
    	commandLine 'xmlstarlet', 'val', '-s', "build/xml/vespucci-preset-${vespucci_preset_version}.xlmns", '-e', input
	}	
}

// special for building vespucci
task vespucciBuild(type: Copy) {
  from projectDir
  include preset_file
  into 'gen'
  filteringCharset = 'UTF-8'
  rename (preset_file, vespucci_preset_build_file)
  expand([
    ICONPATH: 'icons/png/',
    ICONTYPE: 'png'
  ])
}

task generateAndValidateVespucciBuild(dependsOn: ["vespucciBuild","copyResources"], type: Exec) {
	def input = 'gen/' + vespucci_preset_build_file
	mkdir('gen')
	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    	// validation
    	commandLine 'cmd', '/c', 'xmlstarlet', 'val', '-s', "build/xml/vespucci-preset-${vespucci_preset_version}.xlmns", '-e', input
	} else {
    	// validation
    	commandLine 'xmlstarlet', 'val', '-s', "build/xml/vespucci-preset-${vespucci_preset_version}.xlmns", '-e', input
	}	
}

task vespucciZip(dependsOn: ["generateAndValidateVespucci","updateTranslations"], type: Zip) {
    baseName 'vespucci_zip'
    destinationDir = new File(projectDir, 'gen')
    from ('icons/png')
    from ('gen/' + vespucci_preset_file)
    from ('i18n')
    exclude ('*.pot')
}
vespucciZip.group = 'preset'
vespucciZip.description = 'Generate zipped preset file for vespucci'

vespucciZip.group = 'preset'
vespucciZip.description = 'Generate zipped preset file for vespucci'

task vespucciIconsZip(type: Zip) {
    baseName 'vespucci_icons'
    destinationDir = new File(projectDir, 'gen')
    from ('icons/png')
}
vespucciZip.group = 'preset'
vespucciZip.description = 'Generate zipped icon file for vespucci'

task vespucciNoTranslationsZip(dependsOn: ["generateAndValidateVespucci"], type: Zip) {
    baseName 'vespucci_zip_no_translations'
    destinationDir = new File(projectDir, 'gen')
    from ('icons/png')
    from ('gen/' + vespucci_preset_file)
}
vespucciNoTranslationsZip.group = 'preset'
vespucciNoTranslationsZip.description = 'Generate zipped preset file for vespucci without translations'

// generate everything
task generateAllPresetTypes {
	dependsOn "vespucciNoTranslationsZip"
	dependsOn "vespucciZip" 
	dependsOn "vespucciIconsZip" 
	dependsOn "generateAndValidateVespucciBuild"
	dependsOn "josmZip"
	dependsOn "updatePot"
	dependsOn "updateWebsite"
} 
generateAllPresetTypes.group = 'preset'
generateAllPresetTypes.description = 'Generate all preset types'

